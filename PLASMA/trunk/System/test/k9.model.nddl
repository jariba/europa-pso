// /home/sailesh/work/EXPERIMENTS/CPlan1.5/model/k9.ddl

#include "NddlWorld.nddl"

class Location {

  predicate NotTracked {
    Location target;
    eq(object, target);
  }

  predicate trackstart {
    Location target;
    eq(object, target);
  }

  predicate Tracked {
    Location target;
    eq(object, target);
  }

  predicate trackstop {
    Location target;
    eq(object, target);
  }
}

Location::NotTracked {
  meets(object.trackstart tstart);
  met_by(object.trackstop tstop);
}

Location::trackstart {
  meets(object.Tracked t);
  met_by(object.NotTracked nt);
  contained_by(Tracker.LandmarksDefined l);
}


Location::Tracked {
  meets(object.trackstop tstop);
  met_by(object.trackstart tstart);
}

Location::trackstop {
  meets(object.NotTracked nt);
  met_by(object.Tracked t);
}


class Path {
  Location m_from, m_to;

  Path(Location from, Location to) {
    m_from = from;
    m_to = to;
  }  
}

class CHAMP_Accessable {
  Location m_from, m_to;

  CHAMP_Accessable(Location from, Location to) {
    m_from = from;
    m_to = to;
  }  
}

class OppSci_Accesable {
  Location m_from, m_to;

  OppSci_Accesable(Location from, Location to) {
    m_from = from;
    m_to = to;
  }  
}

class Position {  
  Rover m_rover;

  Position(Rover rover) {
    m_rover = rover;
  }

  predicate At {
    Location location;
  }
  
  predicate navigate {
    Location from;
    Location to;
    neq(from, to);
    eq(duration, 1);
  }  
}

Position::At{
  meets(object.navigate a);
  eq(location , a.from);
  
  met_by(object.navigate b);
  eq(location, b.to);
}

    

Position::navigate{

  Path path : {
    eq(path.m_from, from);
    eq(path.m_to, to);
  };

  meets(object.At a);
  eq(to , a.location);
  
  met_by(object.At b);
  eq(b.location, from);

  //not able to reference parents objects more than one level.
  Rover rovers;
  contained_by(Tracker.TrackingOn tOn);
  commonAncestor(tOn.object, object, rovers); 

  contained_by(OpportunisticScience.OppSciIdle oppsci);
  commonAncestor(oppsci.object, object, rovers); 

  contained_by(CHAMP.IPIdle ipidle);
  commonAncestor(ipidle.object, object, rovers); 

}

class Tracker{
  Rover m_rover;

  Tracker(Rover rover) {
    m_rover = rover;
  }

  predicate TrackingOff {
  }

  predicate trackloadgroup {
  }

  predicate LandmarksDefined {
  }

  predicate StartTracking {    
  }

  predicate TrackingOn {
  }

  predicate trackfreeze {
  }

  predicate TrackingFrozen {
  }

  predicate trackunfreeze {
  }

}

Tracker::TrackingOff{
  meets(object.trackloadgroup tlgp);
}

Tracker::trackloadgroup{
  meets(object.LandmarksDefined tld);
  met_by(object.TrackingOff toff);
}

Tracker::LandmarksDefined{
  met_by(object.trackloadgroup tlgp);
  meets(object.StartTracking tst);
}

Tracker::StartTracking{
  met_by(object.LandmarksDefined tld);
  meets(object.TrackingOn tto);
}

Tracker::TrackingOn{
  meets(object.trackfreeze tfz);
  bool OR;
  if(OR == true){
    met_by(object.StartTracking s0);
  }
  if(OR == false){
    met_by(object.trackunfreeze s0);
  }
}

Tracker::trackfreeze{
  met_by(object.TrackingOn tld);
  meets(object.TrackingFrozen tto);
}

Tracker::TrackingFrozen{
  met_by(object.trackfreeze tld);
  meets(object.trackunfreeze tto);
}

Tracker::trackunfreeze{
  met_by(object.TrackingFrozen tld);
  meets(object.TrackingOn tto);
}

class OpportunisticScience {
  Rover m_rover;

  OpportunisticScience(Rover rover) {
    m_rover = rover;
  }

  predicate OppSciIdle {   
  }

  predicate oppscidefineproc {
  }

  predicate OppSciProcDefined {
  }

  predicate oppscisetparams {
  }

  predicate OppSciParamsSet {
  }

  predicate oppscilooknow {
    Location target;
    Location at_loc;
    neq(target, at_loc);
  }

  predicate OppSciDoneLookNow {
    Location  target;
    Location at_loc;
    neq(target, at_loc);
  }

  predicate oppscigetstatus {
    Location  target;
    Location at_loc;
    neq(target, at_loc);
  }
}

OpportunisticScience::oppscidefineproc {
  met_by(object.OppSciIdle s0);
  meets(object.OppSciProcDefined s1);
}

OpportunisticScience::OppSciProcDefined {
  met_by(object.oppscidefineproc s0);
  meets(object.oppscisetparams s1);
}

OpportunisticScience::oppscisetparams {
  met_by(object.OppSciProcDefined s0);
  meets(object.OppSciParamsSet s1);
}

OpportunisticScience::OppSciParamsSet {
  met_by(object.oppscisetparams s0);
  meets(object.oppscilooknow s1);
}

OpportunisticScience::oppscilooknow {
  met_by(object.OppSciParamsSet s0);
  meets(object.OppSciDoneLookNow s1);
  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  OppSci_Accesable oppsci : {
    eq(oppsci.m_from, at_loc);
    eq(oppsci.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingFrozen tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(CHAMP.IPIdle ipidle);
  commonAncestor(ipidle.object, object, rovers); 
    
}
OpportunisticScience::OppSciDoneLookNow {
  met_by(object.oppscilooknow s0);
  meets(object.oppscigetstatus s1);
}

OpportunisticScience::oppscigetstatus {
  met_by(object.OppSciDoneLookNow s0);
  meets(object.OppSciIdle s1);
  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  OppSci_Accesable oppsci : {
    eq(oppsci.m_from, at_loc);
    eq(oppsci.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingFrozen tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(CHAMP.IPIdle ipidle);
  commonAncestor(ipidle.object, object, rovers); 
}

class CHAMP {
  Rover m_rover;

  CHAMP(Rover rover) {
    m_rover = rover;
  }

  predicate IPIdle {
  }
  
  predicate ipgetname {
    Location target;
    Location at_loc;
    ////eq(duration,[1 +inf]);
  }

  predicate IPHaveName {
    Location target;
    Location at_loc;
  }

  predicate ipsettarget {
    Location target;
    Location at_loc;
    //eq(duration,[1 +inf]);
  }
  
  predicate IPTargetSet {
    Location target;
    Location at_loc;
  }

  predicate ipplaceinstrument {
    Location target;
    Location at_loc;
    //eq(duration,[1 +inf]);
  }

  predicate IPDonePlaceInstrument {
    Location target;
    Location at_loc;
  }

  predicate ipgetstatus {
    Location target;
    Location at_loc;
    //eq(duration,[1 +inf]);
  }
}

CHAMP::ipgetname {
  met_by(object.IPIdle s0);
  meets(object.IPHaveName s1);

  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  // Location.Tracked tr : {
  //   eq(tr.object, target);
  // };
  // contained_by(Location.Tracked tr);


  CHAMP_Accessable champ : {
    eq(champ.m_from, at_loc);
    eq(champ.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingOn tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(OpportunisticScience.OppSciIdle champidle);
  commonAncestor(champidle.object, object, rovers); 
}

CHAMP::iIPHaveName {
  met_by(object.ipgetname s0);
  meets(object.ipsettarget s1);
}

CHAMP::ipsettarget {
  met_by(object.IPHaveName s0);
  meets(object.IPTargetSet s1);

  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  CHAMP_Accessable champ : {
    eq(champ.m_from, at_loc);
    eq(champ.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingOn tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(OpportunisticScience.OppSciIdle champidle);
  commonAncestor(champidle.object, object, rovers); 
}

CHAMP::IPTargetSet {
  met_by(object.ipsettarget s0);
  meets(object.ipplaceinstrument s1);

}

CHAMP::ipplaceinstrument {
  met_by(object.IPTargetSet s0);
  meets(object.IPDonePlaceInstrument s1);

  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  CHAMP_Accessable champ : {
    eq(champ.m_from, at_loc);
    eq(champ.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingOn tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(OpportunisticScience.OppSciIdle champidle);
  commonAncestor(champidle.object, object, rovers); 
}


CHAMP::IPDonePlaceInstrument {
  met_by(object.ipplaceinstrument s0);
  meets(object.ipgetstatus s1);

}

CHAMP::ipgetstatus {
  met_by(object.IPDonePlaceInstrument s0);

  contained_by(Location.Tracked tr);
  eq(tr.object, target);

  CHAMP_Accessable champ : {
    eq(champ.m_from, at_loc);
    eq(champ.m_to, target);
  };

  Rover rovers;
  contained_by(Tracker.TrackingOn tfo);
  commonAncestor(tfo.object, object, rovers); 

  contained_by(Position.At at);
  commonAncestor(at.object, object, rovers); 

  contained_by(OpportunisticScience.OppSciIdle champidle);
  commonAncestor(champidle.object, object, rovers); 
}


class Rover {

  Position m_position;
  Tracker m_tracker;
  OpportunisticScience m_oppsci;
  CHAMP m_champ;

  Rover() {    
    m_position = new Position(this) ;
    m_tracker = new Tracker(this) ;
    m_oppsci = new OpportunisticScience (this);
    m_champ = new CHAMP (this);
  }

}


