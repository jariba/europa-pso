enum Locations {Hill, Rock, Lander};

class Rover{
 predicate At{
  Locations m_location;
  eq(duration, 1);
 }

 predicate Going{
  Locations m_from, m_to;
  eq(duration, 4);
  neq(m_from, m_to);
 }
}

Rover::At{
 meets(Going to);
 eq(to.m_from, m_location);

 met_by(Going from);
 eq(from.m_to, m_location);
}

Rover::Going{
  neq(m_from, m_to);
  met_by(At from);
  eq(m_from, from.m_location);
  meets(At to);
  eq(m_to, to.m_location);
}

// Defines the initial state for the problem.
class World {
 // Horizon information
 int m_horizonStart;
 int m_horizonEnd;
 Rover m_rover;

 predicate initialState{}
 World(){
  // Set up the planning horizon
  m_horizonStart = 10;
  m_horizonEnd = 100;
  m_rover = new Rover();
 }
}

World::initialState{
 // Enforce horizon bounds on initialState
 leq(object.m_horizonStart, start);
 leq(end, object.m_horizonEnd);
 
 contains(Rover.At a);
 eq(a.m_location, Lander);
 eq(a.start, 10);

 contains(Rover.At b);
 eq(b.m_location, Rock);
 eq(b.end, 100);

 leq(a.end, b.start);
}