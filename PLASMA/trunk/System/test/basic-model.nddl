enum Locations {Hill, Rock, Lander};

class Rover{
 predicate At{
  Locations m_location;
 }

 predicate Going{
  Locations m_from, m_to;
 }
}

Rover::At{
 eq(duration, 1);
 meets(Going to);
 eq(to.m_from, m_location);

 meets(Going from);
 eq(from.m_to, m_location);
}

Rover::Going{
  neq(m_from, m_to);
  eq(duration, 4);
}

// Defines the initial state for the problem.
class World {
 // Horizon information
 int m_horizonStart;
 int m_horizonEnd;
 Rover m_rover;

 predicate initialState{}
 World(){
  // Set up the planning horizon
  m_horizonStart = 10;
  m_horizonEnd = 100;
  m_rover = new Rover();
 }
}

World::initialState{
 // Enforce horizon bounds on initialState
 leq(object.m_horizonStart, start);
 leq(end, object.m_horizonEnd);
 
 contains(Rover.At a);
 eq(a.m_location, Lander);

 contains(Rover.At b);
 eq(b.m_location, Rock);

 leq(a.end, b.start);
}